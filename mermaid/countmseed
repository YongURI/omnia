#!/bin/csh
#
# Counts miniSEED files for MERMAID data -- similar to countsac except this does
# not also tally .evt and .cp files.
#
# Last modified by fjsimons-at-alum.mit.edu, 02/12/2019
# Last modified by jdsimon@princeton.edu, 02-Sep-2020

set MERMAID = $MERMAID
find $MERMAID/processed/ -maxdepth 3 -type f -name '*mseed' > $MERMAID/all_mseed

# Count only Princeton MSEED files
set floatnum = (08 09 10 11 12 13 16 17 18 19 20 21 22 23 24 25)
set mseed_count = 0
printf "\n               ...Tallying the miniSEED files...                \n"
printf "\n             All      DET       LAST DET      REQ       LAST REQ\n"

# OSX does not have the standard date formatting commands.  Must use a
# pseudo-switch statement.  Redirect any error to the /dev/null/ to
# suppress output to stdout and check if it failed. On a proper Linux
# machine this will display as '01-Jan-0001'; in Mac terminal it breaks.
date -d 00010101 +'%d-%b-%Y' >& /dev/null
if ( $? == 0 ) then
    set osx = 0
else
    set osx = 1
endif

foreach float ($floatnum[*])
    # This tallies the miniSEED files.
    grep "P-$float" $MERMAID/all_mseed > $MERMAID/temp
    set total = `cat $MERMAID/temp | wc -l`
    set det =  `grep .DET. $MERMAID/temp  | wc -l`
    set req =  `grep .REQ. $MERMAID/temp  | wc -l`
    rm $MERMAID/temp

    @ total_count += $total
    @ det_count += $det
    @ req_count += $req

    # This finds the date of the last miniSEED file.
    set perfloat_det = `ls -1 $MERMAID/processed/*P-$float*/*/*.DET.*mseed | tail -1`
    set lasttime_det = `basename $perfloat_det | awk '{print substr($0,1,8)}'`

    ls -1 $MERMAID/processed/*P-$float*/*/*.REQ.*mseed >& /dev/null
    if ( $? == 0 ) then
	set perfloat_req = `ls -1 $MERMAID/processed/*P-$float*/*/*.REQ.*mseed | tail -1`
	set lasttime_req = `basename $perfloat_req | awk '{print substr($0,1,8)}'`
    else

    	set lasttime_req = '19700101'  # Dummy date stamp in case of empty.

    endif

    if ( $osx == 0 ) then
        set lastdate_det = `date -d $lasttime_det +'%d-%b-%Y'`
        set lastdate_req = `date -d $lasttime_req +'%d-%b-%Y'`

    else
	set lastdate_det = `date -jf "%Y%m%d" $lasttime_det +"%d-%b-%Y"`
	set lastdate_req = `date -jf "%Y%m%d" $lasttime_req +"%d-%b-%Y"`

    endif
    printf "Float %s : %5i    %5i    %s    %5i    %s\n" $float $total $det $lastdate_det $req $lastdate_req

end
rm $MERMAID/all_mseed

printf -- "----------------------------------------------------------------\n"
printf "   Total : %5i    %5i                   %5i\n\n" $total_count $det_count $req_count
